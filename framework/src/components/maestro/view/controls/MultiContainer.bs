import "pkg:/source/rLog/rLogMixin.bs"

function init()
  registerLogger("MultiContainer", true)
  m.layoutGroup = m.top.findNode("container_layoutGroup")
  m.updateTimer = m.top.findNode("container_updateTimer")
  m.animation = m.top.findNode("container_animation")
  m.interpolator = m.top.findNode("container_interpolator")
  m.previousY = 0
  
  m.top.observeField("index", "onIndexChange")
  m.top.observeField("size", "onSizeChange")
  m.top.observeField("children", "onChildrenChange")
  m.top.observeField("needsRecalulating", "onNeedsRecalcuatingChange")
  m.updateTimer.observeField("fire", "onNeedsRecalcuatingChange")
  m.previousCurrFocusRow = 1
  onSizeChange()
end function

function initialize(args)
  updateChildren()
end function

function updateChildren(newChildren = invalid)
  children = []
  m.updateTimer.control = "stop"
  
  if newChildren = invalid
    newChildren = m.top.getChildren( - 1, 0)
  end if
  
  for each child in newChildren
    if child.id <> "container_layoutGroup" and child.id <> "container_animation" and child.id <> "container_updateTimer"
      children.push(child)
      if child.doesExist("currFocusRow")
        child.observeField("currFocusRow", "onHeroOffsetChildCurrFocusRowChange")
      end if
      
    end if
  end for
  
  m.layoutGroup.appendChildren(children)
  m.top.numChildren = children.count()
  m.top.index = 0
  onNeedsRecalcuatingChange()
  m.updateTimer.control = "start"
end function

function onHeroOffsetChildCurrFocusRowChange(event)
  'change override offset back to 0
  currFocusRow = event.getData()
  node = event.getroSGNode()
  delta = currFocusRow - m.previousCurrFocusRow
  m.previousCurrFocusRow = currFocusRow
  
  if delta < 0
    direction = -1
  else
    direction = 1
  end if
  
  ' logVerbose("currFocus Row", currFocusRow, "node.itemFocused", node.itemFocused, "delta", delta, "direction", direction, "animationDirection", m.animationDirection)
  
  if node.doesExist("isHeroOffsetApplied")
    ' logInfo("currFocusChange", currFocusRow, "offsetRequired", node.isHeroOffsetApplied = true, "animationDirection", m.animationDirection)
    if node.isHeroOffsetApplied = false and currFocusRow >= 0 and currFocusRow < 1 and direction = -1
      node.isHeroOffsetApplied = true
      logVerbose("ADDING HERO")
      
      moveToCurrentIndex()
      
    else if node.isHeroOffsetApplied = true and currFocusRow < 1 and direction = 1
      logVerbose("REMOVING HERO")
      node.isHeroOffsetApplied = false
      moveToCurrentIndex()
    end if
  end if
end function

function onSizeChange()
  setClippingRectangle()
end function


function setClippingRectangle()
  child = m.layoutGroup.getChild(m.top.index)
  clippingOffsets = invalid
  if child <> invalid
    clippingOffsets = child.containerClippingOffsets
  end if
  
  if clippingOffsets = invalid
    clippingOffsets = [0, 0, 0, 0]
  end if
  
  m.top.clippingRect = [clippingOffsets[0], clippingOffsets[1], m.top.size[0] + clippingOffsets[2], m.top.size[1] + clippingOffsets[3]]
end function

function onChildrenChange()
  m.layoutGroup.removeChildren(m.layoutGroup.getChildren( - 1, 0))
  updateChildren(m.top.children)
  onIndexChange()
  onSizeChange() 
end function

function onIndexChange()
  ' logMethod("onIndexChange", m.top.index, "isAnimated", m.top.isAnimated)
  child = m.layoutGroup.getChild(m.top.index)
  
  if child = invalid
    logWarn("tried to set index before the children were created")
    return invalid
  end if
  
  setClippingRectangle()
  
  if child.heroControl <> invalid and child.itemFocused <> invalid and child.itemFocused < 1
    m.previousCurrFocusRow = 1
    child.isHeroOffsetApplied = true
  end if
  
  ' logVerbose("child.isHeroOffsetApplied", child.isHeroOffsetApplied)
  moveToCurrentIndex()
  
  if m.top.isFocused or m.top.isChildFocused
    setFocus(child)
  end if
  
end function

function moveToCurrentIndex(forceNoAnimation = false)
  newY = getYOfIndex(m.top.index)
  if newY < m.layoutGroup.translation[1]
    m.animationDirection = 1
  else
    m.animationDirection = -1
  end if
  
  ' logInfo("index", m.top.index, "new Y is", newY, "force no animation", forceNoAnimation, "animationDirection", m.animationDirection)
  
  if m.top.isAnimated and not forceNoAnimation
    if m.animation.state = "running"
      m.animation.control = "pause"
    end if
    
    m.interpolator.keyValue = [[0, m.previousY], [0, newY]]
    
    if m.animation.control = "pause"
      m.animation.control = "start"
    else
      m.animation.control = "resume"
    end if
    
  else
    ' logInfo("moving with no animation")
    m.layoutGroup.translation = [0, newY]
  end if
  
  m.previousY = newY
end function

function getYOfIndex(index)
  y = 0
  currentOffset = 0
  ' logVerbose("index", index)
  child = m.layoutGroup.getchild(index)
  
  if child <> invalid and child.heroControl <> invalid and child.isHeroOffsetApplied
    ' logVerbose("child is hero with offset applied")
    ' logVerbose(">>> adding child hero height", child.heroControl.boundingRect().height)
    currentOffset += - child.heroControl.boundingRect().height
  else 
    if index < m.top.yOffsets.count()
      currentOffset -= m.top.yOffsets[index]
    else
      currentOffset -= m.top.yOffsets[m.top.yOffsets.count() - 1]
    end if
    
  end if
  
  ' logVerbose(">> offsets",  m.top.yOffsets)
  
  for i = 0 to index - 1
    ' logVerbose(">>> ",  index,  " currentOffset ",  currentOffset)
    
    child = m.layoutGroup.getChild(i)
    
    if child <> invalid
      ' logVerbose(" child height si ", child.boundingRect().height)
      y += child.boundingRect().height + currentOffset
    else
      ' logWarn("child of MultiContainer with index", i, "is invalid")
    end if
  end for
  
  return - y
end function

function onNeedsRecalcuatingChange()
  logInfo("onNeedsRecalcuatingChange - recalcuating multi container")
  y = 0
  heroControl = invalid
  for each child in m.layoutGroup.getChildren( - 1, 0)
    'TODO observe the bounds.. not gonna be fun.. :(
    child.translation = [0, y]
    y += child.boundingRect().height
    
    MU.setOrAddNodeField(child, "heroControl", heroControl) 
    MU.setOrAddNodeField(child, "isHeroOffsetApplied", child.isHeroOffsetApplied = true) 
    MU.setOrAddNodeField(child, "_multiContainer", m.top) 
    
    if child.isHero = true
      heroControl = child
    else
      heroControl = invalid
    end if
    
  end for
  
end function

function onGainedFocus(isSelfFocused)
  ' logMethod("onGainedFocus", isSelfFocused)
  
  if isSelfFocused
    
    onIndexChange()
    child = m.layoutGroup.getChild(m.top.index)
    if child <> invalid
      setFocus(child)
    end if
  end if
end function
